include(CTest)

add_executable(TestExPaD 
        TestExPaD.cpp 
        macros.hh
        DataType_test.cpp 
        DataType_test.hh 
        RTT_test.cpp
        RTT_test.hh
        ExPlot_test.cpp
        ExPlot_test.hh
)

set(test_dir $<TARGET_FILE_DIR:TestExPaD>)

target_link_libraries(TestExPaD PUBLIC libROOTToText libExPlot)

add_test(NAME TestExPaD COMMAND TestExPaD WORKING_DIRECTORY ${test_dir})

add_test(NAME TestGLE COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_SOURCE_DIR}/run_gle_test.cmake WORKING_DIRECTORY ${test_dir}/output)

set_tests_properties(TestExPaD PROPERTIES FIXTURES_SETUP Files)
set_tests_properties(TestGLE PROPERTIES FIXTURES_REQUIRED Files)

# clear the folders generated by the tests
add_custom_command(
        TARGET TestExPaD
        COMMAND ${CMAKE_COMMAND} -E rm -rf ${test_dir}/output
)

if (MSVC)
        # in Windows, it is necessary to copy the shared library (dll) to the same folder as the executable
        add_custom_command(
                TARGET TestExPaD
                COMMAND ${CMAKE_COMMAND} -E copy
                        $<TARGET_FILE:libROOTToText>
                        $<TARGET_FILE:libExPlot>
                        ${test_dir}
                DEPENDS libROOTToText libExPlot
        )
endif()

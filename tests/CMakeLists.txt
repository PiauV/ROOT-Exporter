include(CTest)

add_executable(TREx 
        TREx.cpp 
        macros.hh
        DataType_test.cpp 
        DataType_test.hh 
        RTT_test.cpp
        RTT_test.hh
        ExPad_test.cpp
        ExPad_test.hh
)

set(test_dir $<TARGET_FILE_DIR:TREx>)
set(out_dir ${test_dir}/output)

target_link_libraries(TREx PUBLIC libExPad)

add_test(NAME TestREx COMMAND TREx WORKING_DIRECTORY ${test_dir})

set (EXT_TEST ${CMAKE_CURRENT_SOURCE_DIR}/run_test_external.cmake)
add_test(NAME TestGLE     COMMAND ${CMAKE_COMMAND} -DTEST=GLE     -P ${EXT_TEST} WORKING_DIRECTORY ${out_dir})
add_test(NAME TestGnuplot COMMAND ${CMAKE_COMMAND} -DTEST=GNUPLOT -P ${EXT_TEST} WORKING_DIRECTORY ${out_dir})
add_test(NAME TestPython  COMMAND ${CMAKE_COMMAND} -DTEST=PYTHON  -P ${EXT_TEST} WORKING_DIRECTORY ${out_dir})

set_tests_properties(TestREx     PROPERTIES FIXTURES_SETUP    Files TIMEOUT 60)
set_tests_properties(TestGLE     PROPERTIES FIXTURES_REQUIRED Files TIMEOUT 100)
set_tests_properties(TestGnuplot PROPERTIES FIXTURES_REQUIRED Files TIMEOUT 100)
set_tests_properties(TestPython  PROPERTIES FIXTURES_REQUIRED Files TIMEOUT 100)

# clear the folders generated by the tests
add_custom_command(
        TARGET TREx
        COMMAND ${CMAKE_COMMAND} -E echo "Cleaning test output folder..."
        COMMAND ${CMAKE_COMMAND} -E rm -rf ${test_dir}/output
)

if (MSVC)
        # in Windows, it is necessary to copy the shared library (dll) to the same folder as the executable
        add_custom_command(
                TARGET TREx
                COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:libExPad> $<TARGET_FILE:libROOTToText> ${test_dir}
                DEPENDS libExPad libROOTToText
        )
endif()

include(CTest)

add_executable(TestExPaD 
        TestExPaD.cpp 
        macros.hh
        DataType_test.cpp 
        DataType_test.hh 
        RTT_test.cpp
        RTT_test.hh
        ExPlot_test.cpp
        ExPlot_test.hh
)

set(test_dir $<TARGET_FILE_DIR:TestExPaD>)
set(out_dir ${test_dir}/output)

target_link_libraries(TestExPaD PUBLIC libExPlot)

add_test(NAME TestExPaD COMMAND TestExPaD WORKING_DIRECTORY ${test_dir})

set (EXT_TEST ${CMAKE_CURRENT_SOURCE_DIR}/run_test_external.cmake)
add_test(NAME TestGLE     COMMAND ${CMAKE_COMMAND} -DTEST=GLE     -P ${EXT_TEST} WORKING_DIRECTORY ${out_dir})
add_test(NAME TestGnuplot COMMAND ${CMAKE_COMMAND} -DTEST=GNUPLOT -P ${EXT_TEST} WORKING_DIRECTORY ${out_dir})
add_test(NAME TestPython  COMMAND ${CMAKE_COMMAND} -DTEST=PYTHON  -P ${EXT_TEST} WORKING_DIRECTORY ${out_dir})

set_tests_properties(TestExPaD   PROPERTIES FIXTURES_SETUP    Files)
set_tests_properties(TestGLE     PROPERTIES FIXTURES_REQUIRED Files)
set_tests_properties(TestGnuplot PROPERTIES FIXTURES_REQUIRED Files)
set_tests_properties(TestPython  PROPERTIES FIXTURES_REQUIRED Files)

# clear the folders generated by the tests
add_custom_command(
        TARGET TestExPaD
        COMMAND ${CMAKE_COMMAND} -E echo "Cleaning test output folder..."
        COMMAND ${CMAKE_COMMAND} -E rm -rf ${test_dir}/output
)

if (MSVC)
        # in Windows, it is necessary to copy the shared library (dll) to the same folder as the executable
        add_custom_command(
                TARGET TestExPaD
                COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:libExPlot> ${test_dir}
                DEPENDS libExPlot
        )
endif()
